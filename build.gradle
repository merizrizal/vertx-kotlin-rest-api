import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = '1.9.21'
    ext.vertx_version = '4.5.1'

    group = 'com.merizrizal'
    version = '1.0.0'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    // Vertx Core
    implementation "io.vertx:vertx-core:$vertx_version"
    implementation "io.vertx:vertx-lang-kotlin:$vertx_version"

    // Vertx Web
    implementation "io.vertx:vertx-web:$vertx_version"

    // Vertx Rxjava
    implementation "io.vertx:vertx-rx-java3:$vertx_version"
    implementation "io.vertx:vertx-rx-java3-gen:$vertx_version"

    // Vertx Postgresql
    implementation "io.vertx:vertx-pg-client:$vertx_version"
    // Scram SASL SCRAM-SHA-256
    implementation 'com.ongres.scram:client:2.1'
}

sourceSets {
    main {
        java {
            srcDirs += [layout.buildDirectory.dir("generated/sources/java")]
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': archiveVersion
        attributes 'Main-Class': 'AppKt'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.register('generateConfig', Copy) {
    from layout.projectDirectory.file("src/main/resources/template/Config.java.template")
    into layout.buildDirectory.dir("generated/sources/java/config")

    def props = new Properties()
    file("env.properties").withInputStream { props.load(it) }

    filter(ReplaceTokens, tokens: [
            'PG_HOST'    : props.getProperty('pg.host'),
            'PG_DATABASE': props.getProperty('pg.database'),
            'PG_USER'    : props.getProperty('pg.user'),
            'PG_PASSWORD': props.getProperty('pg.password')
    ])

    rename 'Config.java.template', 'Config.java'
}

tasks.register('cleanAndJar') {
    group = 'build'
    description = 'Clean and create jar'

    dependsOn clean
    dependsOn jar
}

compileKotlin {
    dependsOn generateConfig
}

compileJava {
    dependsOn generateConfig
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}